10   DEFINT A,S,I,J,K,M,N
20   DIM A(22,22),SUMC(5),KI(5),KJ(5)
30 ON KEY(1) GOSUB 40
40   ICOUNT=0 'initialize counter
50 CLS:KEY OFF
60 LOCATE 25,50: PRINT"-PRESS F1 TO RESTART-"
70 COLOR 15,0: LOCATE 2,15: PRINT "MONTE CARLO ISING MODEL "
80 COLOR 7: PRINT:PRINT "Monte Carlo Statistics for a 20X20 ISING MODEL with"
90 PRINT "            periodic boundary conditions."
100 PRINT: PRINT" The critical temperature is approximately 2.0."
110 PRINT:PRINT "CHOOSE THE TEMPERATURE FOR YOUR RUN.  Type a number between "
120 INPUT"     0.1 and 100, and then press  'ENTER'.",T
130 IF T<.1 THEN T=.1 ELSE IF T>100 THEN T=100
140 PRINT ">>>>> temperature=" T: T=1/T
150 KEY(1) ON
160 PRINT:PRINT "DO YOU WANT TO STUDY THE CORRELATION FUNCTION (Y OR N)?"
170 COR$=INPUT$(1)
180 IF COR$="y" THEN COR$="Y"
190 IF COR$="Y" THEN PRINT ">>>> correlation data will be shown" ELSE PRINT              ">>>> no correlation data will be shown"
200 PRINT:PRINT "PICK THE TYPE OF INITIAL SPIN CONFIGURATION"
210 PRINT,"TYPE c FOR `CHECKERBOARD' PATTERN, OR"
220 PRINT,"TYPE i FOR `INTERFACE' PATTERN"
230 PRINT,"TYPE u FOR `UNEQUAL INTERFACE' PATTERN"
240 X$=INPUT$(1)
250 IF X$="C" OR X$="c" GOTO 370
260 IF X$="u" THEN X$="U"
270 IF X$="i" THEN X$="I"
280 IF X$="I" OR X$="U" THEN 290 ELSE 210 'ROUTING TO PROPER INITIAL SETUP
290 CLS 'initial INTERFACE setup
300   IF X$="U" THEN MAXJ=14 ELSE MAXJ=10
310   FOR I=0 TO 22
320   FOR J=0 TO MAXJ: A(I,J)=+1: NEXT
330   FOR J=MAXJ+1 TO 22: A(I,J)=-1: NEXT
340   A(I,0)=-1: A(I,21)=1:
350   NEXT
360 GOTO 420
370 CLS 'INITIAL checkerboard PATTERN
380   A(0,0)=1
390   FOR I=0 TO 20: A(I+1,0)=-A(I,0)
400   FOR J=0 TO 20: A(I,J+1)=-A(I,J):NEXT
410   NEXT
420 REM initial display:
430       LOCATE 25,50:PRINT"-PRESS F1 TO RESTART-"
440       FOR I=1 TO 20
450       FOR J=1 TO 20
460       FOR JZ=2*J-1 TO 2*J
470       LOCATE I,JZ: IF A(I,J)=1 THEN PRINT CHR$(219) ELSE PRINT CHR$(176)
480       NEXT JZ,J,I
490       LOCATE 10,50: PRINT"TEMP="1/T
500 TIME$="00:00:00"
510 IF X$="U" THEN NPLUS=280 ELSE NPLUS=200
520 IF COR$="Y" THEN GOSUB 710
530 M=INT(20*RND+1):N=INT(20*RND+1): S=-A(N,M): ICOUNT=ICOUNT+1 '**flip a spin
540 B=T*S*(A(N-1,M)+A(N+1,M)+A(N,M-1)+A(N,M+1))*2
550 IF EXP(B)<RND GOTO 620                            'test against random#
560 A(N,M)=S: NPLUS=NPLUS+S
570 IF N=1 THEN A(21,M)=S ELSE IF N=20 THEN A(0,M)=S
580 IF M=1 THEN A(N,21)=S ELSE IF M=20 THEN A(N,0)=S
590  FOR IX=2*M-1 TO 2*M  'update the display
600  LOCATE N,IX:IF A(N,M)=1 THEN PRINT CHR$(219) ELSE PRINT CHR$(176)
610  NEXT
620 LOCATE 23,21: PRINT ICOUNT: LOCATE 23,30: PRINT TIME$
630 IF (ICOUNT MOD 100)=0 THEN GOSUB 670
640 IF COR$="Y" AND (ICOUNT MOD 400)=0 THEN GOSUB 750
650 GOTO 530
660 END
670 LOCATE 12,50: PRINT "AT "ICOUNT;
680 XN=NPLUS/400!
690 PRINT USING "  N+/N=.###";XN
700 RETURN
710 LOCATE 14,47: PRINT "Correlation Function:"
720 LOCATE 15,51: PRINT "d  <s(0)s(d)>": LOCATE 16,50: PRINT"-------------"
730 GOSUB 750
740 RETURN
750 FOR M=1 TO 5: SUMC(M)=0:   'Correlation calculation
760 LOCATE 14,69: PRINT"(at "ICOUNT")"
770 FOR I=1 TO 20
780 FOR J=1 TO 20:KJ=(J+M) MOD 20: KI=(I+M) MOD 20: CC%=A(KI,J)+A(I,KJ)
790 IF CC%=0 THEN GOTO 810
800 SUMC(M)=SUMC(M)+A(I,J)*CC%
810 NEXT J,I
820 LOCATE 16+M,50: PRINT M: LOCATE 16+M,54: PRINT USING "+#.###"; SUMC(M)/800
830 NEXT M: RETURN
